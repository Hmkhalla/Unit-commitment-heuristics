reset;
model unit_commitment.mod;
data 100_24_01_w.dat;
option solver cplex;

# Run the solver with time limit 
option cplex_options 'nodefile=2 \
       mipdisplay=2 \
       mipinterval=100 \
       file=model.lp \
       timelimit=5 \
       logfile=cplex.log';

param best_of default Infinity;
param best_y{j in J, t in T};
param best_z{j in J, t in T};
param best_v{j in J, t in T};
param best_p{j in J, t in T};	
param best_p_max{j in J, t in T};	

for {iter in 1..5} {
  solve;
  printf "#####################################################################\n";
  printf "##   Iteration %d, of = %f, best_of = %f   ##\n", iter, cost_function, best_of;
  printf "#####################################################################\n"; 
  if cost_function < best_of then {
    let best_of := cost_function;
    let {j in J, t in T} best_y[j,t] := y[j,t];
    let {j in J, t in T} best_z[j,t] := z[j,t];
    let {j in J, t in T} best_v[j,t] := v[j,t];
    let {j in J, t in T} best_p[j,t] := p[j,t];
    let {j in J, t in T} best_p_max[j,t] := p_max[j,t];
    printf "\n ******* Iteration %d, updated best_of = %f\n\n", iter, best_of;
  }
  unfix v;
  let {j in J, t in T} y[j,t] := best_y[j,t];
  let {j in J, t in T} z[j,t] := best_z[j,t];
  let {j in J, t in T} v[j,t] := best_v[j,t];
  let {j in J, t in T} p[j,t] := best_p[j,t];
  let {j in J, t in T} p_max[j,t] := best_p_max[j,t];
  # RANDOM strategy
  for {j in J, t in T: best_v[j,t]==1} {
    if Uniform(0,1) <= 0.5 then {
      fix v[j,t] := 1;
    }
  }
}

printf "\n#####################################################################\n";
printf "#####                          Final solve                        #####\n";
printf "#####################################################################\n\n";
# provide to the solver the best solution found so far as a starting point
unfix v;
let {j in J, t in T} y[j,t] := best_y[j,t];
let {j in J, t in T} z[j,t] := best_z[j,t];
let {j in J, t in T} v[j,t] := best_v[j,t];
let {j in J, t in T} p[j,t] := best_p[j,t];
let {j in J, t in T} p_max[j,t] := best_p_max[j,t];

# Run the solver with time limit 3600 seconds (1 hour)
option cplex_options 'nodefile=2 \
       mipdisplay=2 \
       mipinterval=100 \
       file=model.lp \
       timelimit=3600 \
       logfile=cplex.log';
expand > expand_last_problem.txt;
solve;

display cost_function;
display _total_solve_time;

